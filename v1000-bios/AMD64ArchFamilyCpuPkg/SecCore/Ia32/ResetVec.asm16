;** @file
;
;   Reset Vector Data structure.
;   This structure is located at 0xFFFFFF00 (FIX_UP_ZERO_VECTOR).
;
;******************************************************************************
;* Copyright (c) 2013, Insyde Software Corp. All Rights Reserved.
;*
;* You may not reproduce, distribute, publish, display, perform, modify, adapt,
;* transmit, broadcast, present, recite, release, license or otherwise exploit
;* any part of this publication in any form, by any means, without the prior
;* written permission of Insyde Software Corporation.
;*
;******************************************************************************

    .model  tiny
    .686p
    .stack  0h
    .code

FIX_UP_ZERO_VECTOR EQU 0FFFFFF00h

;
; Reserve area for SEC Padding
;
    ORG     0h
    DD      FixedPcdGet32 (PcdSecPad1Raw32)
    DD      FixedPcdGet32 (PcdSecPad2Raw32)
    DB      (0FFFFFFD0h - FIX_UP_ZERO_VECTOR) DUP (0)

;
; The layout of below is fixed. The build tool makes assumption of the layout.
;
    ORG     0FFFFFFD0h - FIX_UP_ZERO_VECTOR
;
; This is located at 0xFFFFFFD0h
;
    mov     di, "AP"
    jmp     ApStartup

    ORG     0FFFFFFE0h - FIX_UP_ZERO_VECTOR
;
; Pointer to the entry point of the PEI core
; It is located at 0xFFFFFFE0, and is fixed up by some build tool
; So if the value 8..1 appears in the final FD image, tool failure occurs.
;
PeiCoreEntryPoint       DD      87654321h

;
; This is the handler for all kinds of exceptions. Since it's for debugging
; purpose only, nothing except a deadloop would be done here. Developers could
; analyze the cause of the exception if a debugger had been attached.
;
InterruptHandler    PROC
    jmp     $
    iret
InterruptHandler    ENDP

    ORG     0FFFFFFF0h - FIX_UP_ZERO_VECTOR
;
; For IA32, the reset vector must be at 0xFFFFFFF0, i.e., 4G-16 byte
; Execution starts here upon power-on/platform-reset.
;
ResetHandler:
    nop
    nop
ApStartup:
    ;
    ; Jmp Rel16 instruction
    ; Use machine code directly in case of the assembler optimization
    ; SEC entry point relatvie address will be fixed up by some build tool.
    ;
    ; Typically, SEC entry point is the function _ModuleEntryPoint() defined in
    ; SecEntry.asm
    ;
    DB      0e9h
    DW      -3


    ORG     0FFFFFFF8h - FIX_UP_ZERO_VECTOR
;
; Ap reset vector segment address is at 0xFFFFFFF8
; This will be fixed up by some build tool,
; so if the value 1..8 appears in the final FD image,
; tool failure occurs
;
ApSegAddress    dd      12345678h

    ORG     0FFFFFFFCh - FIX_UP_ZERO_VECTOR
;
; BFV Base is at 0xFFFFFFFC
; This will be fixed up by some build tool,
; so if the value 1..8 appears in the final FD image,
; tool failure occurs.
;
BfvBase     DD      12345678h

;
; Nothing can go here, otherwise the layout of this file would change.
;

    END
